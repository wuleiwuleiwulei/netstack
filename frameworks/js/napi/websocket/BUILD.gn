# Copyright (c) 2021-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/communication/netstack/netstack_config.gni")

ohos_shared_library("websocket") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  branch_protector_ret = "pac_ret"

  include_dirs = [
    "async_context/include",
    "async_work/include",
    "constant/include",
    "websocket_exec/include",
    "websocket_module/include",
  ]

  sources = [
    "async_context/src/close_context.cpp",
    "async_context/src/connect_context.cpp",
    "async_context/src/send_context.cpp",
    "async_work/src/websocket_async_work.cpp",
    "constant/src/constant.cpp",
    "websocket_exec/src/websocket_exec.cpp",
    "websocket_module/src/websocket_module.cpp",
  ]

  defines = [ "OHOS_LIBWEBSOCKETS=1" ]

  cflags = [
    "-fstack-protector-strong",
    "-D_FORTIFY_SOURCE=2",
    "-O2",
  ]

  cflags_cc = [
    "-fstack-protector-strong",
    "-D_FORTIFY_SOURCE=2",
    "-O2",
  ]

  deps = [ "$NETSTACK_DIR/utils/napi_utils:napi_utils" ]

  external_deps = [
    "hilog:libhilog",
    "libwebsockets:websockets",
    "napi:ace_napi",
    "openssl:libcrypto_shared",
    "openssl:libssl_shared",
    "samgr:samgr_proxy",
  ]

  if (defined(global_parts_info) &&
      defined(global_parts_info.communication_netmanager_base) &&
      global_parts_info.communication_netmanager_base) {
    external_deps += [
      "netmanager_base:net_conn_manager_if",
      "netmanager_base:net_security_config_if",
    ]
    defines += [ "HAS_NETMANAGER_BASE=1" ]
  } else {
    defines += [ "HAS_NETMANAGER_BASE=0" ]
  }

  defines += [ "NETSTACK_WEBSOCKETSERVER" ]
  sources += [
    "async_context/src/list_all_connections_context.cpp",
    "async_context/src/server_close_context.cpp",
    "async_context/src/server_send_context.cpp",
    "async_context/src/server_start_context.cpp",
    "async_context/src/server_stop_context.cpp",
    "websocket_exec/src/websocket_server_exec.cpp",
  ]

  if (product_name != "ohos-sdk") {
    external_deps += [ "init:libbegetutil" ]
  }

  relative_install_dir = "module/net"
  part_name = "netstack"
  subsystem_name = "communication"
}
