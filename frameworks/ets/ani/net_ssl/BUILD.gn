# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//foundation/communication/netstack/netstack_config.gni")
import("//build/ohos.gni")

rust_cxx("network_security_ani_cxx") {
  sources = [ "src/wrapper.rs" ]
}

ohos_static_library("network_security_ani_static") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "${target_gen_dir}/src",
    "$NETSTACK_DIR/frameworks/native/net_ssl/include",
  ]
  sources = [ "src/cxx/network_security_ani.cpp" ]
  sources += get_target_outputs(":network_security_ani_cxx")
  
  deps = [
    ":network_security_ani_cxx",
    "$NETSTACK_DIR/interfaces/innerkits/net_ssl:net_ssl",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "netmanager_base:net_conn_manager_if",
    "netmanager_base:net_security_config_if",
    "rust_cxx:cxx_cppdeps",
  ]

  part_name = "netstack"
  subsystem_name = "communication"
}

ohos_rust_shared_library("network_security_ani") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    boundary_sanitize = true
    all_ubsan = true
    debug = false
  }

  sources = [ "src/lib.rs" ]
  
  external_deps = [
    "runtime_core:ani",
    "hilog:hilog_rust",
    "netmanager_base:ani_rs",
    "rust_cxx:lib"
  ]
  deps = [
    ":network_security_ani_static",
    "../common:netstack_common",
    "//third_party/rust/crates/serde/serde:lib"
  ]
  clippy_lints = "none"
  part_name = "netstack"
  subsystem_name = "communication"
  output_extension = "so"
}

generate_static_abc("network_security") {
  base_url = "./ets"
  files = [ "ets/@ohos.net.networkSecurity.d.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/network_security.abc"
}

ohos_prebuilt_etc("network_security_etc") {
  source = "$target_out_dir/network_security.abc"
  module_install_dir = "framework"
  part_name = "netstack"
  subsystem_name = "communication"
  deps = [ ":network_security" ]
}
